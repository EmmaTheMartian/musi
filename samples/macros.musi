# musi supports powerful macros which can alter the AST of your program.
# Macros take in a list of tokens and return a list of tokens.

# This macro does nothing
let noop = macro tokens do
	return tokens
end

@noop(println('Hello, World!'))

# This is a neat macro that returns the provided code as their tokens
# This macro exists in the `macros` module
let quote = macro tokens do
	let processed = [{kind='literal', value='['}]
	tokens -> lists.each(fn it do
		lists.append(processed, {kind='literal', value='{'})
		lists.append(processed, {kind='id', value='kind'})
		lists.append(processed, {kind='operator', value='='})
		lists.append(processed, {kind='str', value=it.kind})
		lists.append(processed, {kind='id', value='value'})
		lists.append(processed, {kind='operator', value='='})
		lists.append(processed, {kind='str', value=it.value})
		lists.append(processed, {kind='literal', value='}'})
		lists.append(processed, {kind='literal', value=','})
	end)
	processed -> lists.append({kind='literal', value=']'})
	return processed
end

println(@quote(fn do return null end))

# This macro creates a lambda function!
# This macro exists in the `macros` module
let lambda = macro tokens do
	let processed = [{kind='keyword', value='fn'}]

	let found_pipe = false
	tokens -> lists.ieach(fn index, token do
		if token.kind == 'operator' && token.value == '->' do
			found_pipe = true
			lists.append(processed, {kind='keyword', value='do'})
			lists.append(processed, {kind='keyword', value='return'})
			return null
		end
		lists.append(processed, token)
	end)

	lists.append(processed, {kind='keyword', value='end'})

	return processed
end

let newuser = @lambda(name, age -> { name=name, age=age })
println(newuser('Gandalf', 50000))
